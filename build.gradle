plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'java'
  id 'io.freefair.lombok' version '8.10.2'
  id 'io.spring.dependency-management' version '1.1.5'
  id 'org.springframework.boot' version '3.3.5'
  id 'com.github.kt3k.coveralls' version '2.12.2'
  id 'se.patrikerdes.use-latest-versions' version '0.2.18'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'org.sonarqube' version '5.1.0.4882'
  id 'uk.gov.hmcts.java' version '0.12.63'
  id 'org.owasp.dependencycheck' version '11.0.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

application {
  mainClass = 'uk.gov.hmcts.ccd.casemigration.CaseMigrationRunner'
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task fortifyScan(type: JavaExec)  {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.3'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

sonarqube {
  properties {
    property "sonar.projectName", "ccd-case-migration-starter"
    property "sonar.projectKey", "ccd-case-migration-starter"
    property "sonar.exclusions", "**/exception/*.java,**/domain/*.java,**/common/*.java,**/migration/auth/AuthTokenGeneratorConfiguration.java,**/migration/CaseMigrationRunner.java,**/ccd/HttpMessageConverterConfiguration.java"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

apply from: './gradle/suppress.gradle'
// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext {
  set('spring-framework.version', '6.1.14')
  set('spring-security.version', '6.3.4')
  set('springCloudVersion', '2023.0.3')
  set('jackson.version', '2.18.0')
  set('snakeyaml.version', '2.3')
  log4JVersion = "2.24.1"
  reformLogging= '6.0.1'
  junitJupiterVersion = '5.10.5'
  junitVintageVersion = '5.10.5'
  powermockVersion = '2.0.9'
}

dependencies {
  // SPRING
  implementation group: 'org.springframework', name: 'spring-context-support'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core'
  implementation group: 'org.springframework.security', name: 'spring-security-config'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.8.0'

  // HMCTS
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.3'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.2.0'
  implementation group: 'com.github.hmcts', name: 'ccd-client', version: '5.0.3'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLogging

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
  implementation group: 'commons-io', name: 'commons-io', version: '2.17.0'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.33.2'
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitJupiterVersion
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
  testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: junitVintageVersion
  testImplementation group:'org.mockito', name: 'mockito-junit-jupiter', version:'3.12.4'
  testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powermockVersion
  testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.9.3'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.3'

  
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

bootJar {
  archiveFileName = 'ccd-case-migration.jar'
  archiveBaseName = 'ccd-case-migration'
  archiveVersion = project.version.toString()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
  timeout = Duration.ofMinutes(30)
  environment("AZURE_APPLICATIONINSIGHTS_INSTRUMENTATIONKEY", "some-key")
  systemProperty 'java.locale.providers', 'COMPAT'
  failFast = true

  useJUnitPlatform()

  testLogging {
    events "failed"
    exceptionFormat "short"

    debug {
      events "passed", "started", "skipped", "failed"
      exceptionFormat "full"
    }

    info.events = ["failed", "skipped"]
  }

  reports {
    html.required = true
  }

  jvmArgs = [
    '--add-modules', 'java.se',
    '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED',
    '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
    '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
    '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
    '--add-opens', 'java.management/sun.management=ALL-UNNAMED',
    '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED'
  ]
}
